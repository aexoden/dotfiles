export DOTFILES=$HOME/.dotfiles

#-------------------------------------------------------------------------------
# Local Environment Variables
#-------------------------------------------------------------------------------

if [[ -a ~/.localrc ]] ; then
	source ~/.localrc
fi

#-------------------------------------------------------------------------------
# Load Configuration
#-------------------------------------------------------------------------------

# Find all zsh files.
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# Load all files which modify $PATH.
for file in ${(M)config_files:#*/path.zsh} ; do
	source $file
done

# Load all files other than path and completion files.
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh} ; do
	source $file
done

# Initialize autocomplete.
autoload -U compinit
compinit

# Load completion files.
for file in ${(M)config_files:#*/completion.zsh} ; do
    source $file
done

unset config_files
